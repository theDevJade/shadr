/*
 * Copyright © 2026 theDevJade
 *
 * Licensed under the Apache License, Version 2.0. See LICENSE.
 * SPDX-License-Identifier: Apache-2.0
 */
#define SHADR_FIELD_RANGE 4.0

in float shadrMode;

bool shadr_is_field() {
    return shadrMode > 1.5;
}

float shadr_median(vec3 field) {
    return max(min(field.r, field.g), min(max(field.r, field.g), field.b));
}

vec3 shadr_sample_field(sampler2D atlas, vec2 uv, vec2 atlasSize) {
    vec2 texel = uv * atlasSize - 0.5;
    vec2 f = fract(texel);
    vec2 base = floor(texel) + 0.5;

    vec3 s00 = textureLod(atlas, (base + vec2(0.0, 0.0)) / atlasSize, 0.0).rgb;
    vec3 s10 = textureLod(atlas, (base + vec2(1.0, 0.0)) / atlasSize, 0.0).rgb;
    vec3 s01 = textureLod(atlas, (base + vec2(0.0, 1.0)) / atlasSize, 0.0).rgb;
    vec3 s11 = textureLod(atlas, (base + vec2(1.0, 1.0)) / atlasSize, 0.0).rgb;

    return mix(mix(s00, s10, f.x), mix(s01, s11, f.x), f.y);
}

float shadr_field_alpha(vec3 field, float texelsPerPixel) {
    float distance = (shadr_median(field) - 0.5) * SHADR_FIELD_RANGE;
    return clamp(distance / max(texelsPerPixel, 0.0001) + 0.5, 0.0, 1.0);
}

float shadr_texels_per_pixel(vec2 uvDerivative, vec2 atlasSize) {
    return max(max(uvDerivative.x * atlasSize.x, uvDerivative.y * atlasSize.y), 0.0001);
}
